#al c chat;
#al * chat*;
#al r reply;

#al {lm} {
    map;
};

#al {grep} {
    #grep %1;
};

#al {le} {l east};
#al {lw} {l west};
#al {ls} {l south};
#al {ln} {l north};

#al {i} {
    #send {i %0};
    lock 总计;
};

#al {sc} {
    #send {score %0};
    lock 内力修为;
};

#al {jifa} {
    #send {jifa %0};
    lock 以下是你目前使用中的特殊技能;
};

#al {lock} {
    #if {"%0" != ""} {
        #ac {%0} {
            #ac {^>} {
                #buffer lock;
                #unac {^>}
            };
            #unac {%0};
        };
    };
};

#al {heal.stop} {
    #unticker {heal};
};

#al {heal.start} {
    #ti {heal} {
        yun lifeheal %1;
        #edelay {2} {
            dazuo 100;
        };
    } {5};
};

#al {on_hit} {
    #al tmp_hit #cr;
    #al tmp_hit %0;
    #ac {你战胜了} {
        #unac {你战胜了};
        #delay {2} {
            halt;
            tmp_hit;
        };
    };
};

#al {on_dead} {
    #al tmp_dead #cr;
    #al tmp_dead %0;
    #ac {死了} {
        #unac {死了};
        #delay {2} {
            halt;
            tmp_dead;
        };
    } {1};
};

#al {on_sleep} {
    #al sleep_action #cr;
    #al sleep_action %0;
    #class on_wakeup.inner open;
    #ac {翻来覆去无法入眠!} {
        #delay {sleep} {
            sleep;
        } {10};
    };
    #ac {睡过一觉} {
        #delay {sleep} {
            sleep;
        } {10};
    };
    #ac {你一觉醒来} {
        #class on_wakeup.inner kill;
        #delay {wakeup} {
            sleep_action;
        } {3};
    };
    #ac {荡悠悠三更梦 -} {
        #class on_wakeup.inner kill;
        #delay {wakeup} {
            sleep_action;
        } {3};
    };
    #class on_wakeup.inner close;
    gt ${sleep_pos};
    on_there {
        sleep;
    };
};

#al {log.save} {
    #system {cp debug${dot}${char}${dot}log ${char}${dot}log};
};

#al {log.start} {
    #log overwrite debug${dot}${char}${dot}log;
    #debug all log;
    #var debug_info on;
    update_status;
};

#al {log.stop} {
    #log off;
    #debug all off;
};

#al {walk_to} {
    #var  direction %1;
    #var  action #cr;
    #al {get_pos_done} {
        #list exits_list create ${exits_list};
        #if {@ismember{${direction};exits_list}} {
            @abbrev{${direction}};
        };
        #else {
            #foreach {${exits_list}[%*]} {exit} {
                #regexp {${exit}} {%*${direction}%*} {#var action ${exit}};
            };
            @abbrev{${action}};
        };
    };
    get_pos {
        get_pos_done;
    };
};

#macro {\e[1;3A} {#nop ];
walk_to north;
};
#macro {\e[1;3B} {#nop ];
walk_to south;
};
#macro {\e[1;3D} {#nop ];
walk_to west;
};
#macro {\e[1;3C} {#nop ];
walk_to east;
};

#al {chain} {
    on_unbusy {%1 {on_unbusy {%2 {on_unbusy {%3 {on_unbusy {%4 {on_unbusy {%5 {on_unbusy {%6 {on_unbusy {%7 {on_unbusy {%8 {on_unbusy {%9 {on_unbusy {%10 {on_unbusy {%11 {on_unbusy {%12;};};};};};};};};};};};};};};};};};};};};};};};
};

#function {py} {
    INVOKE pinyin %0;
};

#al {n} {north;look};
#al {s} {south;look};
#al {e} {east;look};
#al {w} {west;look};
#al {u} {up;look};
#al {d} {down;look};
#al {ne} {northeast;look};
#al {sw} {southwest;look};
#al {nw} {northwest;look};
#al {se} {southeast;look};
#al {nd} {northdown;look};
#al {su} {southup;look};
#al {nu} {northup;look};
#al {sd} {southdown;look};
#al {eu} {eastup;look};
#al {wd} {westdown;look};
#al {ed} {eastdown;look};
#al {wu} {westup;look};

#list kaimen_actions create {open;push;pull;break};
#list kaimen_names create {door;gate;men;stone};
#al {kaimen} {
    #gag 什么？;
    #foreach {${kaimen_actions}[%*]} {kaimen_action} {
        #foreach {${kaimen_names}[%*]} {kaimen_name} {
            #show ${kaimen_action} ${kaimen_name};
            ${kaimen_action} ${kaimen_name};
        };
    };
    #delay {1} {
        #ungag 什么？;
    };
};

#macro {\eL} {
    show.reset;
};

#al {INVOKE} {
    #script {file_name} {python3 -m bin.%0 ${char}};
    #var file_name ${file_name}[1];
    #if {"${file_name}" != ""} {
        #read ${file_name};
        #system {rm ${file_name}};
    };
};

#al {check_heart_beat} {
    #delay {check_heart_beat_delay} {
        #if {${pid} != 0} {
            #system {kill -30 ${pid}};
            end_and_restart;
        };
    } {600};
};

#event {SESSION DEACTIVATED} {
    #if {"%0" == "mud"} {
        #if {${pid} != 0} {
            #system {kill -30 ${pid}};
            end_and_restart;
        };
    };
};

#al {end_and_restart} {
    #end;
};

#al {end} {
    #unevent {SESSION DEACTIVATED};
    unset_last_quest;
    #end;
};

#al {quit_and_restart} {
    #class quit.inner.open;
    #ac {你丢下} {
        #class quit.inner kill;
        #end;
    };
    #ti {quit} {
        #send {quit};
    } {3};
    #class quit.inner.close;
    #send {quit};
};

#al {quit} {
    #unevent {SESSION DEACTIVATED};
    unset_last_quest;
    quit_and_restart;
};

#al {set_last_quest} {
    #var last_quest %1;
    @setenv{last_quest;${last_quest}};
};

#al {unset_last_quest} {
    #var last_quest nil;
    @setenv{last_quest;nil};
};

#al {restart_last_quest} {
    #var last_quest @getenv{last_quest};
    #if {"${last_quest}" != "nil"} {
        ${last_quest}${dot}start;
    };
};

#al {notify} {
    irc.notify %0;
};

#al {corpse} {
    get all from corpse;
};

#al {on_server_reboot} {
    #showme INFO: 服务器重启!;
};

#al {shifu} {
    gt $shifu_pos;
};

#al walk {
    #al tmp_walk_to #cr;
    #al tmp_walk_to %0;

    #class on_walk open;
    #ac {^%S - } {
        #class on_walk kill;
        #undelay walk_delay;
        #showme {ROOM_CHANGED};
    };
    #delay walk_delay {
        #class on_walk kill;
        #showme {ROOM_NOT_CHANGED};
    } {3};
    #class on_walk close;

    tmp_walk_to;
};

#al {chifan} {
    #al chifan_action #cr;
    #al chifan_action %0;
    #class chifan open;
    #al {chifan_done} {
        #class chifan kill;
        chifan_action;
    };
    #ac {你身上没有这样东西。} {
        get_wan {
            eat wan;
        };
    };
    #ac {你捏起一颗小小的辟谷丸往舌下一放。} {
        chifan_done;
    };
    #class chifan close;
    
    #if {${food_percent} < 20 || ${water_percent} < 20} {
        eat wan;
    };
    #else {
        chifan_done;
    };
};

#al {cun_qn} {
    #al cun_qn_action #cr;
    #al cun_qn_action %0;

    #class cun_qn.inner open;
    #ac {你储存了%*点潜能！} {
        #class cun_qn.inner kill;
        #math saved_qn %%1;
        @setenv{qn;${saved_qn}};
        #send {out};
        #send {southwest};
        #delay {1} {
            #showme CUN_QN_DONE;
            cun_qn_action;            
        };        
    };
    #class cun_qn.inner close;
    #if {${qn} <= 1000} {
        #showme CUN_QN_DONE;
        cun_qn_action;            
        #return;
    };
    gt 潜能银行;
    on_there {
        deposit ${qn};
        account;
    };
};

#al {qu_qn} {
    #al qu_qn_action #cr;
    #al qu_qn_action %0;

    #class qu_qn.inner open;
    #ac {你储存了%*点潜能！} {
        #class qu_qn.inner kill;
        #math saved_qn %%1;
        @setenv{qn;${saved_qn}};
        #send {out};
        #send {southwest};
        #delay {1} {
            #showme QU_QN_DONE;
            qu_qn_action;            
        };
    };
    
    #class qu_qn.inner close;
    gt 潜能银行;
    on_there {
        withdraw %1;
        account;
    };
};

#al {gift.start} {
    #var gift_ticker 2;
    #var gift_ticker %1;
    #ti {gift} {
        play; place; play; yun refresh; yun recover;
    } {${gift_ticker}};
};

#al {gift.stop} {
    #unticker {gift};
};

#al {yun} {exert};
#al {cun} {deposit};
#al {qu} {withdraw};

#function {getenv} {
    #var result @mapget{env;%1};
};

#function {setenv} {
    @mapset{env;%1;%2};
    #var result #cr;
    #delay {set_env_delay} {
        #class env write char/${char}_env${dot}tin;
    }{3};
};

#list garbage_list create {lian zi;pigu wan};
#list garbage_list size garbage_size;

#al {drop_garbage} {
    #al drop_garbage_action #cr;
    #al drop_garbage_action %0;

    #class drop_garbage open;
    #al {drop_next_garbge} {
        #math garbage_index ${garbage_index}+1;
        #if {${garbage_index} > ${garbage_size}} {
            #class drop_garbage kill;
            #delay {2} {
                #send {up};
                drop_garbage_action;                
            };
            #return;
        };
        #list garbage_list get ${garbage_index} current_garbage;
        #ti {drop_garbage} {
            drop ${current_garbage};
        } {1};
    };
    #ac {你身上没有这样东西。} {
        #unti {drop_garbage};
        #delay {1} {
            drop_next_garbge;    
        };
    };
    #class drop_garbage close;
    
    #var garbage_index 0;
    gt 瓦砾场;
    on_there {
        drop_next_garbge;        
    };
};
