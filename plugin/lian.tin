#class lian open;
#var lian_count 10;
#alias {lian.start} {
    show_to_quest 练习;
    lian.setup;
    get_next_to_lian;
    #if {"${skill_to_lian}" != "nil"} {
        chain chifan start_lian;
        wd.start {lian.start};
    };
    #else {
        lian.stop;
    };
};

#alias {lian.stop} {
    stop_lian_ticker;
    #class lian.inner kill;
    show_to_quest 练习结束;
    wd.stop;
};

#alias {lian.setup} {
    #class lian.inner open;
    #var lian_index 2;
    #alias {get_next_to_lian} {
        #math lian_index ${lian_index}+1;
        #if {${lian_index} > ${skills_size}} {
            #var skill_to_lian nil;
            #return;
        };
        #list skills get ${lian_index} skill_to_lian;
        #var lian_level_up 0;
        show_to_quest 练习: ${skill_to_lian};
        #var x @get_level_a{${skill_to_lian}};
        #var y @get_level_b{${skill_to_lian}};
        #if {${x} <= ${y} || ${x} ==0 || ${y} == 0} {
            get_next_to_lian;
        }; 
    };

    #var lian_level_up 0;
    #action {你的「%*」进步了！} {
        #math lian_level_up ${lian_level_up}+1;
        show_to_info %%1 +${lian_level_up};
    } {1};

    #action {你的%*进步了} {
        wd.kick;
    } {2};
    
    #alias {start_lian} {
        gt ${shifu_pos};
        on_there {
            start_lian_ticker;
        };
    };

    #alias {start_lian_ticker} {
        #ticker {lian} {
            lian ${skill_to_lian} ${lian_count};
        } {1};
        #ticker {lian_check_status} {
            lian_check_status;
        } {5};
    };
    
    #alias {stop_lian_ticker} {
        #unticker {lian};
        #unticker {lian_check_status};
    };

    #alias {lian_check_status} {
        #if {${nl_percent} < 10} {
            #if {${js_percent} < 30} {
                chain on_sleep chifan start_lian;
                #return;
            };
            #if {${qx_percent} < 30} {
                stop_lian_ticker;
                chain chifan dz.full start_lian;
            };
        };
        #else {
            re;
        };
    };
    
    #ticker {check_level} {
        #if {@get_level_a{${skill_to_lian}} <= @get_level_b{${skill_to_lian}}} {
            stop_lian_ticker;
            get_next_to_lian;
            #if {"${skill_to_lian}" != "nil"} {
                start_lian_ticker;
            };
            #else {
                lian.stop;
            };
        };        
    } {60};
    
    #action {必须空手} {
        unwield all;
    };

    #class lian.inner close;
};

#class lian close;
